resource "aws_s3_bucket" "lake" {
    bucket = "${var.project_name}-lake-${local.account_id}"
    force_destroy = true
}

resource "aws_s3_bucket_versioning" "lake" {
    bucket = aws_s3_bucket.lake.id
    versioning_configuration {
        status = "Enabled"
    }
}

resource "aws_s3_bucket_public_access_block" "lake" {
    bucket = aws_s3_bucket.lake.id
    block_public_acls = true
    block_public_policy = true
    ignore_public_acls = true
    restrict_public_buckets = true
}

resource "aws_s3_bucket_server_side_encryption_configuration" "lake" {
    bucket = aws_s3_bucket.lake.id
    rule{
        apply_server_side_encryption_by_default{
            sse_algorithm = "AES256"
        }
    }
}

resource "aws_s3_bucket_lifecycle_configuration" "lake" {
    bucket = aws_s3_bucket.lake.id
    rule {
        id = "abort-incomplete-multipart"
        status = "Enabled"
        filter {}
        abort_incomplete_multipart_upload {
            days_after_initiation = 1
        }
    }
    rule {
        id = "expire-athena-results"
        status = "Enabled"
        filter {
            prefix = "athena-results/"
        }
        expiration {
            days = 7
        }
    }
}

output "lake_bucket" {
    description = "Name of the data lake bucket."
    value = aws_s3_bucket.lake.bucket
}